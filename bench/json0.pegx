# This is an input for the benchmark of the parsing machine. This file
# might evolve differently from the one on the standard lib and that's
# fine. Its counterpart without any capture operators should be kept
# in sync though.

JSON      <- _ Value

Value     <- (%Object
            / %Array
            / %Number
            / %String
            / %True
            / %False
            / %Null) _

Object    <- CBO (%Pair (COMA %Pair)*)? CBC
Pair      <- %String COLON JSON
Array     <- ARO (JSON (COMA JSON)*)? ARC

String    <- QUOTE %{ (!QUOTE .)* } QUOTE
Number    <- %{ MINUS? [0-9]+ (DOT [0-9]+)? }

True      <- 'true'
False     <- 'false'
Null      <- 'null'

QUOTE     <- '"'
MINUS     <- '-'
DOT       <- '.'
COLON     <- ':' _
COMA      <- ',' _
CBO       <- '{' _
CBC       <- '}' _
ARO       <- '[' _
ARC       <- ']' _

_         <- Space*
Space     <- ' ' / '\t' / EndOfLine
EndOfLine <- '\r\n' / '\n' / '\r'
