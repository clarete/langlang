Calculator      <- _ %Term !.

Term            <- %Factor ((%PLUS / %MINUS) %Factor)*
Factor          <- %Power ((%STAR / %SLASH) %Power)*
Power           <- %Unary ((%POWER / %MOD) %Unary)*
Unary           <- (%PLUS / %MINUS)? %Primary

Primary         <- PROP %Term PRCL^Close / %Number^Primary
Number          <- %BIN / %HEX / %FLOAT / %DEC

# Terminals
DEC             <- %{ [0-9]+ }             _
FLOAT           <- %{ [0-9]* '.' [0-9]+ }  _
BIN             <- '0b' %{ [0-1]+ }        _
HEX             <- '0x' %{ [0-9a-fA-F]+ }  _

# Arithmetic
PLUS            <- '+'   _
MINUS           <- '-'   _
STAR            <- '*'   _
SLASH           <- '/'   _
MOD             <- '%'   _
POWER           <- '**'  _
PROP            <- '('   _
PRCL            <- ')'   _

## Whitespace Handling
_               <- (Space / Comment)*
Comment         <- '#' (!EndOfLine .)* EndOfLine
Space           <- ' ' / '\t' / EndOfLine
EndOfLine       <- '\r\n' / '\n' / '\r'
EndOfFile       <- !.
