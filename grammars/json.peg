// https://www.rfc-editor.org/rfc/rfc8259

JSON    <- _ Value !.
Value   <- FALSE / NULL / TRUE / Object / Array / Number / String
Object  <- OBO (Member (COMA Member)*)? OBC
Member  <- String COLON Value
Array   <- ARO (Value (COMA Value)*)? ARC

// Whitespaces are not allowed after the MINUS sign
Number  <- '-'? Int Frac? Exp? _
Int     <- '0' / ([1-9][0-9]*)
Frac    <- DOT [0-9]+
Exp     <- [eE][-+]?[0-9]+

String  <- DQUOTE Char* DQUOTE _
Char    <- BSLASH DQUOTE
         / BSLASH BSLASH
         / BSLASH SLASH
         / BSLASH [bfnrt]
         / BSLASH 'u' Hex Hex Hex Hex
         / (!DQUOTE .)

Hex     <- [0-9A-Fa-f]

BSLASH  <- '\\'
SLASH   <- '/'
DQUOTE  <- '"'

TRUE    <- "true"  _
FALSE   <- "false" _
NULL    <- "null"  _

COMA    <- ',' _
COLON   <- ':' _
DOT     <- '.' _
OBO     <- '{' _
OBC     <- '}' _
ARO     <- '[' _
ARC     <- ']' _

_       <- (' ' / '\n' / '\r' / '\t')*
